/*
Animation of the three-body problem

A sample animation which are defined by: 
- the state transition function which determines the state at time t + dt by the state at time t, and
- the rendering function which draws the state.
*/
module Main;

import Funim;
import Funim.Machine;
import Cairo;
import Math;
import MiscAlgos.Geometry;

// FPS
c_fps : F64;
c_fps = 60.0;

// Screen size
c_image_size : (I64, I64);
c_image_size = (800, 600);

// Gravitational constant
gravity : F64;
gravity = 20.0;

// State of a celestial body
type Body = struct {
    pos : Vec2,
    vel : Vec2,
    mass : F64,
};

// Animation state
type State = Array Body;

namespace State {
    // Initial state
    initial : State;
    initial = [
        Body {
            pos : Vec2::make(100.0, 0.0),
            vel : Vec2::make(0.0, 20.0),
            mass : 100.0,
        },
        Body {
            pos : Vec2::make(100.0, 0.0).rotated(2.0 * pi64 / 3.0),
            vel : Vec2::make(0.0, 20.0).rotated(2.0 * pi64 / 3.0),
            mass : 100.0,
        },
        Body {
            pos : Vec2::make(100.0, 0.0).rotated(4.0 * pi64 / 3.0),
            vel : Vec2::make(0.0, 20.0).rotated(4.0 * pi64 / 3.0),
            mass : 100.0,
        },
    ];

    // Update the state
    update : TimeDelta -> State -> State;
    update = |dt, state| (
        range(0, 3).fold([], |i, new_state|
            // Split the list of celestial bodies into the body to update and the others
            let this = state.@(i);
            let others = range(0, 3).filter(|j| j != i).map(|j| state.@(j));
            
            // Calculate the force acting on 'this'
            let force = others.fold(Vec2::make(0.0, 0.0), |other, force|
                let r = other.@pos - this.@pos;
                let d = r.@length;
                let f = r.scaled(1.0 / (d * d * d)).scaled(this.@mass * other.@mass * gravity);
                force + f
            );

            // Update the acceleration, velocity, and position of 'this'
            let acc = force;
            let vel = this.@vel + acc.scaled(dt);
            let pos = this.@pos + vel.scaled(dt);

            // Add the new state
            new_state.push_back(Body {
                pos : pos,
                vel : vel,
                mass : this.@mass,
            })
        )
    );

    // Render the state
    render : State -> Cairo -> IO ();
    render = |state, cairo| (
        cairo.set_source_rgb((1.0, 1.0, 1.0));;
        state.to_iter.fold_m((), |body, _|
            cairo.new_sub_path;;
            cairo.arc(body.@pos.to_tuple, 5.0, 0.0, 2.0 * pi64)
        );;
        cairo.fill
    );
}

main : IO ();
main = (
    let config = Funim::Config {
        fps : c_fps,
        size : c_image_size,
    };
    let state = Machine::from_transition(
        State::initial,
        |dt, s| let s = s.update(dt); (s, s)
    );
    let ani = state.map(State::render);

    Funim::run_animation(ani, config)
);